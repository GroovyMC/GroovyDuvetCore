buildscript {
    dependencies {
        classpath libs.tinyremapper
    }
}

plugins {
    id 'groovy'
    id 'maven-publish'
    id 'signing'
    alias libs.plugins.fabric.loom
    alias libs.plugins.quilt.licenser
    alias libs.plugins.mdg.plugin
    alias libs.plugins.simpleci
    alias libs.plugins.nexuspublish
}

project.buildDir.mkdirs()

tasks.changelog {
    gitDir = project.file('./')
    start = '2.1.13'
}

import org.eclipse.jgit.api.Git

versioning {
    fromTag.set('2.1.13')
}

final calculatedVersion = Git.open(project.file("./")).withCloseable {
    versioning.calculateVersion(it) { cm, info -> }
}

println("${project.name} version: ${project.version = calculatedVersion.toString()}")

license {
    rule project.file('header.txt')
    exclude '**/*.mcmeta'
    exclude '**/package-info.java'
}

afterEvaluate {
    /* tasks.withType(AbstractArchiveTask).configureEach {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
        fileMode = dirMode = 0664
    } */
}

static String getGitCommit() {
    def proc = 'git rev-parse --short HEAD'.execute()
    proc.waitFor()
    if (proc.exitValue()) {
        throw new RuntimeException("Failed to get git commit: ERROR(${proc.exitValue()})")
    }
    return proc.text.trim()
}

static String getGitCommitDate() {
    def procDate = 'git log -1 --format=%at'.execute()
    procDate.waitFor()
    if (procDate.exitValue()) {
        throw new RuntimeException("Failed to get git commit time: ERROR(${procDate.exitValue()})")
    }
    long timestamp = procDate.text.trim() as long * 1000
    return new Date(timestamp).format(/yyyy-MM-dd HH:mm:ssZ/, TimeZone.getTimeZone("UTC"))
}

configurations {
    jacksonResolve
    includeCompileOnlyApi
    modCompileOnlyApi.extendsFrom includeCompileOnlyApi
    include.extendsFrom includeCompileOnlyApi

    includeBundle
    modCompileOnly.extendsFrom includeBundle
    include.extendsFrom includeBundle
}

configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.version == '<jackson-resolve>') {
            details.useVersion configurations.jacksonResolve.resolvedConfiguration.resolvedArtifacts.find {
                it.moduleVersion.id.group == details.requested.group && it.moduleVersion.id.name == details.requested.name
            }.moduleVersion.id.version
        }
    }
}

// Fix TinyRemapper
configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.module.with {it?.name == 'tiny-remapper' && it?.group == 'net.fabricmc'}) {
            details.useTarget libs.tinyremapper
        }
    }
}

repositories {
    // For TinyRemapper sans log spam
    maven {
        name = "Luke's Maven"
        url = 'https://maven.lukebemish.dev/releases'
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    mavenCentral()
}

modsDotGroovy {
    dslVersion = libs.versions.mdg.dsl.get()
    platform 'fabric'
}

dependencies {
    jacksonResolve libs.groovy.toml

    minecraft libs.minecraft
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${libs.versions.parchment.minecraft.get()}:${libs.versions.parchment.mappings.get()}@zip")
    }
    modImplementation libs.fabric.loader

    annotationProcessor libs.autoextension
    annotationProcessor libs.autoservice
    compileOnly libs.autoextension
    compileOnly libs.autoservice

    includeBundle libs.mappingio

    includeCompileOnlyApi libs.groovy.core
    includeCompileOnlyApi libs.groovy.contracts
    includeCompileOnlyApi libs.groovy.datetime
    includeCompileOnlyApi libs.groovy.nio
    includeCompileOnlyApi libs.groovy.macro.core
    includeCompileOnlyApi libs.groovy.macro.library
    includeCompileOnlyApi libs.groovy.templates
    includeCompileOnlyApi libs.groovy.xml
    includeCompileOnlyApi libs.groovy.typecheckers
    includeCompileOnlyApi libs.groovy.dateutil
    includeCompileOnlyApi libs.groovy.ginq
    includeCompileOnlyApi libs.groovy.toml
    includeCompileOnlyApi libs.groovy.json

    includeBundle libs.jackson.core
    includeBundle libs.jackson.annotations
    includeBundle libs.jackson.databind
    includeBundle libs.jackson.dataformat.toml
}

tasks.named('jar', Jar) {
    from(project.file("LICENSE")) {
        rename { "${it}-${archivesBaseName}" }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
}

groovydoc {
    use = true
    exclude 'mods.groovy'
}

tasks.register('groovydocJar', Jar) {
    dependsOn groovydoc
    archiveClassifier.set 'javadoc'
    from groovydoc.destinationDir
}

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
    groovyOptions.optimizationOptions.groovydoc = true
    it.groovyOptions.javaAnnotationProcessing = true
    it.exclude('mods.groovy')
}

tasks.compileGroovy { t ->
    t.options.compilerArgs += [
            "-Aautoextension.name=${project.name}",
            "-Aautoextension.version=${project.version}"
    ]
}

jar {
    manifest.attributes([
            'Specification-Title': 'GroovyDuvet',
            'Specification-Vendor': 'Luke Bemish',
            'Specification-Version': 1,
            'Implementation-Title':"${archivesBaseName}",
            'Implementation-Version': project.version,
            'Implementation-Vendor' : 'Luke Bemish',
            'Implementation-Commit-Time': getGitCommitDate(),
            'Implementation-Commit': getGitCommit()
    ])
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            artifact tasks.groovydocJar
            artifact(changelog.output.get().asFile) {
                it.builtBy changelog
                it.extension = 'txt'
                it.classifier = 'changelog'
            }

            pom {
                name = "GroovyDuvetCore"
                packaging = 'jar'
                description = 'Core language provider allowing mods written in groovy to run on the quilt loader'
                url = 'https://github.com/GroovyMC/GroovyDuvetCore'
                inceptionYear = '2023'

                licenses {
                    license {
                        name = 'LGPL 3.0 or later'
                        url = 'https://opensource.org/license/lgpl-3-0/'
                    }
                }
                developers {
                    developer {
                        id = 'groovymc'
                        name = 'GroovyMC'
                        email = 'holdings@groovymc.org'
                        url = 'https://github.com/GroovyMC/'
                    }
                }
                scm {
                    connection='scm:git:git://github.com/GroovyMC/GroovyDuvetCore.git'
                    url='https://github.com/GroovyMC/GroovyDuvetCore'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username.set(System.getenv('SONATYPE_USER') ?: '')
            password.set(System.getenv('SONATYPE_PASSWORD') ?: '')
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
        }
    }
}

if (System.getenv('SONATYPE_USER')) {
    signing {
        final signingKey = System.getenv('SIGNING_KEY') ?: ''
        final signingPassword = System.getenv('SIGNING_PASSWORD') ?: ''
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}
